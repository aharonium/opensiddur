<?xml version="1.0" encoding="utf-8"?>
<!--
test-html.xsl2 for the OpenSiddur project.
Copyright 2011 Marc Stober and licensed under the GNU LGPL.

This outputs HTML for proofreading from an interim XML format.

colors: http://colorschemedesigner.com/#3k31TK-hWpBpB
-->
<xsl:stylesheet version="2.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

  <xsl:output method="html" indent="yes" />

  <!-- default identity template -->
  <!-- TODO still needed? -->
  <xsl:template match="@* | text()">
    <xsl:copy>
      <xsl:apply-templates select="@* | node()"/>
    </xsl:copy>
  </xsl:template>

  <xsl:template match="pages">
    <!-- HTML5 DOCTYPE -->
    <xsl:text disable-output-escaping="yes">&lt;!DOCTYPE html&gt;&#xA;</xsl:text>
    <html>
      <head>
	<!-- make sure browsers see this as UTF-8 -->
	<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
	<!-- Make sure iPhone reflows the text to its screen size 
	     (otherwise it defaults to a zoomed-out 980px viewport). 
	     See: http://developer.apple.com/library/IOs/#documentation/AppleApplications/Reference/SafariWebContent/UsingtheViewport/UsingtheViewport.html  -->
	<meta name = "viewport" content = "initial-scale = 1.0" />
	<style type="text/css">
	  <!-- todo more readable body size and line height? 
	       see http://www.alistapart.com/articles/howtosizetextincss -->
	  h1 { margin: 5px 0; }
	  .verse-number { 
	    color: #BBB;
	  }
	</style>
      </head>
      <body>
	<div style="margin-bottom: 5px">
	  Page number relative to this file only:
	  <xsl:for-each select="page">
	    <xsl:element name="a">
	      <xsl:attribute name="href">
		<xsl:text>#p</xsl:text>
		<xsl:value-of select="@id" />
	      </xsl:attribute>
	      <xsl:value-of select="@id" />
	    </xsl:element>
	    <xsl:text> </xsl:text>
	  </xsl:for-each>
	</div>

	<xsl:apply-templates select="*"/>
      </body>
    </html>
  </xsl:template>

  <xsl:template match="*">
    <div style="border: solid 2px #dcf; margin-bottom: 4px">
      <div style="background-color: #dcf; padding-bottom: 4px">
	<strong><xsl:value-of select="local-name()" /></strong>
	<xsl:apply-templates select="@*" />
      </div>
      <div style="padding: 5px">
	<xsl:apply-templates select="node()" />
      </div>
    </div>
  </xsl:template>

  <xsl:template match="page">
    <xsl:element name="a">
      <xsl:attribute name="name">
	<xsl:text>p</xsl:text>
	<xsl:value-of select="@id" />
      </xsl:attribute>
    </xsl:element>
    <hr />
    <div style="max-width: 800px; margin: 0 auto">
      <xsl:apply-templates select="header | column" />
      <div style="border-top: solid 1px #8F0700; margin: 10px 0; padding: 5px 0">
	<xsl:apply-templates select="column/footnotes" />
      </div>
      <xsl:apply-templates select="footer" />
    </div>
  </xsl:template>


  <xsl:template match="column">
    <!-- suppress -->
    <xsl:apply-templates select="node()[name() != 'footnotes']" />
  </xsl:template>

  <xsl:template match="header | footer">
    <div style="border: solid 1px #8F0700; margin: 10px 0; padding: 5px; text-align: center">
      <xsl:apply-templates select="node()" />
    </div>
  </xsl:template>

  <xsl:template match="footnotes">
      <xsl:apply-templates select="node()" />
  </xsl:template>


  <xsl:template match="@*">
    <xsl:text> </xsl:text>
    <xsl:value-of select="local-name()" />
    <xsl:text>=</xsl:text>
    <xsl:value-of select="." />  
  </xsl:template>

  <!-- supress empty text nodes -->
  <xsl:template match="text[normalize-space() = '']">
  </xsl:template>

  <xsl:template match="paragraph-break">
    <div style="color: #bbb"><xsl:text>&#182;</xsl:text></div>
  </xsl:template>

  <!-- regular text -->
  <xsl:template match="text[not(@size) and not(@font)]">
    <xsl:apply-templates select="node()" />
  </xsl:template>

  <!-- chapter-number -->
  <xsl:template match="text[@size='25.961']">
    <h2 style="color: #8F0700">Chapter <xsl:apply-templates select="node()" /></h2>
    <span class="verse-number">1</span> <!-- insert first verse number -->
  </xsl:template>

  <!-- verse-number -->
  <xsl:template match="text[@size='6.327']">
    <span class="verse-number"><xsl:apply-templates select="node()" /></span>
  </xsl:template>

  <xsl:template match="text[@size='10.046' and @font='Times New Roman,Italic']">
    <i><xsl:apply-templates select="node()" /></i>
  </xsl:template>

  <xsl:template match="text[@size='6.215' and @font='Times New Roman,Bold']">
    <span style="font-size: 80%"><xsl:apply-templates select="node()" /></span>
  </xsl:template>

  <!-- TODO this mixes book headings, parsha headings, and subsequent page heading that also have book name -->
  <xsl:template match="text[(@size='18.216') or ( @size='21.816')]">
    <h1 style="color: #8F0700"><xsl:apply-templates select="node()" /></h1>
  </xsl:template>

</xsl:stylesheet>
