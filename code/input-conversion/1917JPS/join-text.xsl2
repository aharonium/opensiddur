<?xml version="1.0" encoding="utf-8"?>
<!--
join-text.xsl2 for the OpenSiddur project.
Copyright 2011 Marc Stober and licensed under the GNU LGPL.

Join adjacent single-character text elements
with the same font and size. 
-->
<xsl:stylesheet version="2.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

  <xsl:output method="xml" indent="yes"/>
  <xsl:strip-space elements="*" />
  <xsl:preserve-space elements="text" />

  <!-- default identity template -->
  <xsl:template match="@* | node()">
    <xsl:copy>
      <xsl:apply-templates select="@* | node()"/>
    </xsl:copy>
  </xsl:template>

  <!-- for each text element -->
  <xsl:template match="text">
    <!-- if not preceded by a text element with same font and size -->
    <!-- if (font != pre-font) or (size != pre-size) -->
    <xsl:variable name="preceding-text" select="preceding-sibling::text[1]" />
    <xsl:if test="not(@font = $preceding-text/@font) or not(@size = $preceding-text/@size)">
      <!-- start a new text element -->
      <xsl:element name="text">

	<!-- ** output content ** -->
	<!-- don't output the default font -->
	<xsl:if test="@font != 'Times New Roman'">
	  <xsl:apply-templates select="@font" />
	</xsl:if>
	<!-- don't output the default font size -->
	<xsl:if test="@size != '10.035'">
	  <xsl:apply-templates select="@size" />
	</xsl:if>
	<xsl:variable name="font" select="@font" />
	<xsl:variable name="size" select="@size" />
	<xsl:call-template name="join-same">
	  <xsl:with-param name="font" select="$font" />
	  <xsl:with-param name="size" select="$size" />
	</xsl:call-template>
      </xsl:element>
    </xsl:if>
  </xsl:template>

  <!-- called recursively to output adjacent elements -->
  <xsl:template name="join-same">
    <xsl:param name="font" />
    <xsl:param name="size" />
    <xsl:if test="(@font = $font) and (@size = $size)">
      <xsl:value-of select="text()" />
      <xsl:variable name="font" select="@font" />
      <xsl:variable name="size" select="@size" />
      <xsl:for-each select="following-sibling::text[1]">
	<xsl:call-template name="join-same">
	  <xsl:with-param name="font" select="$font" />
	  <xsl:with-param name="size" select="$size" />
	</xsl:call-template>
      </xsl:for-each>
    </xsl:if>
  </xsl:template>

</xsl:stylesheet>
